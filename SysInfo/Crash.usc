ctrl TabCtrlEM {
	group "Complex";

	GetStdSize() { return Size(150, 100); }

//	Font SetFont = StdFont();
	bool AcceptCurrent = false;
//	Qtf  SetInfo @1 ? "Info of control" ;

	Paint(w) {
		r = GetRect();
		x0 = x = r.left;
		y = r.top;
		tsz = PaintTab(w, x, y, "Tab 1", :SWhiteGray);
		x += tsz.cx;
		x += PaintTab(w, x, y + 3, "Tab 2", :SLtGray).cx;
		x += PaintTab(w, x, y + 3, "Tab 3", :SLtGray).cx;
		r.top += tsz.cy;
		DrawButton(w, r);
		PaintTab(w, x0, y, "Tab 1", :SWhiteGray);
		if(IsUHD())
			w.DrawRect(2, r.top - 2, tsz.cx, 4, :SWhiteGray);
		else
			w.DrawRect(1, r.top - 1, tsz.cx + 2, 2, :SWhiteGray);
	}
};

ctrl ButtonEM {
	group "Push";

	GetMinSize() { sz = XMinSize(); sz.cy += 2; return sz; }
	GetStdSize() { sz = GetMinSize(); sz.cx *= 7; return sz; }

	Text   SetLabel ? "Button label";
	Font   SetFont = StdFont();
	Frame  SetFrame @1;
	Text   Tip @1;

	Paint(w) {
		r = GetRect();
		DrawCtrlFrame(w, r, .SetFrame);
		DrawButtonFrame(w, r);
		sz = Size(r.right - r.left, r.bottom - r.top);
		n = int(sz.cy / 8);
		w.DrawRect(r.left, r.top, sz.cx, sz.cy, :SWhiteGray);
		textsize = GetSmartTextSize(.SetLabel, .SetFont);
		py = (sz.cy - textsize.cy) / 2;
		if(count(.SetLabel) && (.SetLabel)[0] == 1)
			w.DrawSmartText(r.left, r.top + py, .SetLabel, .SetFont, :SBlack, sz.cx);
		else {
			px = (sz.cx - textsize.cx) / 2;
			w.DrawSmartText(px + r.left, py + r.top, .SetLabel, .SetFont, :SBlack, sz.cx);
		}
	}
}